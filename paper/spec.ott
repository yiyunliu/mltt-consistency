grammar

var, x, y :: '' ::= {{ com variables }}

nat, i, j, n, m :: '' ::= {{ com natural numbers }}
  | 0 :: :: zero
  | 1 :: :: numone
  | Suc i :: :: succ {{ tex 1 + [[i]] }}
  | ( i ) :: S :: Paren
  | | G | :: S :: Length
  | (# i #) :: S :: SecretParen
    {{ tex [[i]] }}
  | ren xi i :: S :: renapp {{ tex [[xi]]([[i]]) }}


set, R , S, F, I :: '' ::= {{ com sets }}
  | emptyset :: :: empty
    {{ tex \varnothing }}
  | { a | formula } :: :: comprehension
    {{ tex \{ [[a]] \mid [[formula]]  \} }}
  | { ( a , S ) | formula } :: :: pcomprehension
    {{ tex \{ ([[a]], [[S]]) \mid [[formula]]  \} }}
  | dom S :: :: domain
    {{ tex [[dom]]([[S]]) }}
  | F a   :: :: App
    {{ tex [[F]]([[a]]) }}
  | SVar  :: :: SetOfVars
    {{ tex \textit{Var} }}
  | STm   :: :: SetOfTerms
    {{ tex \textit{Term} }}
  | PowerSet S :: :: PowerSet
    {{ tex \mathcal{P}([[S]]) }}
  | S1 -> S2 :: :: FunSet
    {{ tex [[S1]] \rightarrow [[S2]] }}
  | (# S #) :: :: HParen
    {{ tex [[S]] }}
  | { i | formula  } :: :: comprehensionNat
    {{ tex \{ [[i]] \mid [[formula]]  \} }}
  | SNat :: :: SetofNats
    {{ tex \mathbb{N} }}
  | S0 * S1 :: :: CartesianProd
    {{ tex [[S0]] \times [[S1]] }}
  | S n     :: :: AppInt
    {{ tex [[S]]([[n]]) }}

rho {{ tex \rho }}, xi {{tex \xi}} :: '' ::= {{ com substitutions }}
  | ( rho )      :: :: Parens
  | emptyS     :: :: emptyS
  | id        :: :: id
  | rho .: x -> A  :: :: cons
    {{ tex [[rho]] [ [[x]]\mapsto[[A]] ] }}
  % | xi .: i  :: :: consInt
  %   {{ tex [[xi]]:[[i]] }}
  | idtm          :: :: idtm
    {{ tex \ottkw{id} }}

tm, a , b , c, p, A, B, C, e,f :: 'a_' ::= {{ com terms and types }}
  | ind a (x y . b) c :: :: Ind
    {{ com natural number induction }}
  | arg :: :: Hole
    {{ tex - }}
  | x :: :: Var
    {{ com variable }}
  | Pi x : A . B :: :: Pi
    {{ tex  \Pi [[x]]\!:\![[A]].[[B]] }}
    {{ com dependent function type }}
  | Sigma x : A . B :: :: Sigma
    {{ tex  \Sigma [[x]]\!:\![[A]].[[B]] }}
    {{ com dependent pair type }}
  | Nat :: :: Nat
    {{ com natural number type }}
  | zero  :: :: Zero
    {{ com zero }}
  | succ a  :: :: Successor
    {{ com successor }}
  | \ x . a :: :: Abs
    {{ tex \lambda [[x]]. [[a]] }}
    {{ com function }}
  | \- x : A . a :: :: AnnAbs
    {{ tex \lambda [[x]]\!:\![[A]] . [[a]] }}
    {{ com annotated functions }}
  | a b :: :: App
    {{ tex [[a]] \; [[b]] }}
    {{ com function application }}
  | Set i :: :: Set
    {{ com universe }}
    {{ tex [[Set]]_{[[i]]} }}
  | a { A / x } :: S :: Subst {{ tex [[a]] \{ [[A]] / [[x]]  \} }}
  | a < rho > :: S :: RenRho {{ tex [[a]] \{ [[rho]]  \} }}
  | a { rho } :: S :: SubstRho {{ tex [[a]] \{ [[rho]]  \} }}
  | a { b0 , b1 / x , y } :: S :: SubstTwo {{ tex [[a]] \{ [[b0]] , [[b1]] / [[x]] , [[y]] \} }}
  | ( A )   :: M :: Paren {{ coq ([[A]]) }}
  | (# a #) :: S :: SecretParen
    {{ tex [[a]] }}
    {{ com parsing precedence is hard }}
  | (@ a @) :: M :: TexParen
    {{ tex ([[a]]) }}
  | a ~ b : A :: :: Eq
    {{ com identity type }}
    {{ tex [[a]] \sim [[b]] \in {[[A]]} }}
  | refl      :: :: Refl
    {{ com reflexivity proof }}
  | Void      :: :: Void
    {{ com empty type }}
  | absurd b  :: :: Absurd
    {{ com ex falso quodlibet }}
  | J c p         :: :: Transp
    {{ com J eliminator }}
  | Bool           :: :: Bool
    {{ com boolean type }}
  | true :: :: True
  | false :: :: False
  | if a b0 b1 :: :: If
    {{ tex \ottkw{if} \; [[a]]\; \ottkw{then}\; [[b0]]\; \ottkw{else}\; [[b1]] }}
  % | G x :: S :: lookup
  %   {{ tex [[G]]([[x]]) }}
  | rho x :: S :: lookuprho
    {{ tex [[rho]]([[x]]) }}
  | A -> B :: M :: Arr
    {{ tex [[A]] \rightarrow [[B]] }}

context, G {{ tex \Gamma }}, D {{ tex \Delta }} :: 'ctx_' ::= {{ com contexts }}
					  {{ coq list ( atom * (grade * tm) ) }}
%   | drop n G        ::    :: Drop {{ tex [[G]] \downarrow_{[[n]]} }}
   | empty           ::    :: Empty       {{ coq nil }}
%   | A               :: M   :: One
   | G ++ x : A          ::   :: Append      {{ tex [[G]], [[x]]:[[A]] }}
   | G0 ++ G1        ::   :: Append2     {{ tex [[G0]], [[G1]] }}
   | ( G )           :: M  :: Paren       {{ coq ([[G]]) }}
   | (# G #)           :: M  :: SecretParen {{ tex [[G]] }}

terminals :: 'terminals_' ::=
  | <->               ::   :: iso           {{ tex \leftrightarrow }}
  | <=>               ::   :: lrarrow       {{ tex \Leftrightarrow }}
  | -->               ::   :: step          {{ tex \longrightarrow }}
  | min               ::   :: min           {{ tex \mathsf{min} }}
  | ==                ::   :: definitional  {{ tex \equiv }}
  | all               ::   :: forall     {{ tex \forall }}
  | in                ::   :: in         {{ tex \in }}
  | let               ::   :: let        {{ tex \ottkw{let} }}
  | bind              ::   :: bind       {{ tex \ottkw{bind} }}
  | notin             ::   :: notin      {{ tex \not\in }}
  | <=                ::   :: check      {{ tex \leq }}
  | =>                ::   :: infer      {{ tex \Rightarrow }}
  | =>+               ::   :: minfer      {{ tex \Rightarrow^{\ast} }}
  | ->                ::   :: arrow      {{ tex \to }}
  | <-|               ::   :: mapsfrom   {{ tex \mapsfrom }}
  | []                ::   :: box        {{ tex \Box }}
  | |-                ::   :: entails    {{ tex \vdash }}
  | |-e               ::   :: entailsE   {{ tex \vdash }}
  | |-e               ::   :: entailsEE  {{ tex \vdash }}
  | |-$               ::   :: entailsC   {{ tex \vdash_{C} }}
  | -|                ::   :: produces   {{ tex \dashv }}
  | |=                ::   :: hs_entails {{ tex \vDash }}
  | |==               ::   :: hs_entaill {{ tex \vDash }}
  | /=                ::   :: neq        {{ tex \neq }}
  | |>                ::   :: cast       {{ tex \triangleright }}
  | ok                ::   :: ok         {{ tex \ \mathsf{ok} }}
  | _                 ::   :: blank      {{ tex \_ }}
  | ~>                ::   :: produce    {{ tex \leadsto }}
  | ~>*               ::   :: multistep  {{ tex \leadsto^{\ast} }}
  | ~>h               ::   :: steph      {{ tex \leadsto }}
  | empty             ::   :: empty      {{ tex \cdot }}
  | fv                ::   :: fv         {{ tex \mathsf{fv}\! }}
  | dom               ::   :: dom        {{ tex \mathsf{dom} }}
  | ~                 ::   :: twiddle    {{ tex \sim }}
  | ~~                ::   :: compatible {{ tex \asymp }}
  | '|'               ::   :: bar        {{ tex | }}
  | o                 ::   :: bullet     {{ tex \bullet }}
  | fst               ::   :: fst
  | snd               ::   :: snd
  | |=>|              ::   :: erasesto   {{ tex \mathrel{|\Rightarrow|} }}
  | |-=               ::   :: turneq     {{ tex \vdash_{=} }}
  | refl2             ::   :: refl2      {{ tex \mathbf{refl_2}  }}
  | ++                ::   :: concat     {{ tex \mathop{++} }}
  | *                 ::   :: times      {{ tex \cdot }}
  | Box               ::   :: Box        {{ tex \ottkw{Box} }}
  | =beta             ::   :: betaeq     {{ tex =_{\beta} }}
  | ;                 ::   :: semi       {{ tex \mathsf{;} }}
  | Pi                ::   :: Pi         {{ tex \Pi }}
  | inj               ::   :: inj        {{ tex \ottkw{inj} }}
  | case              ::   :: case       {{ tex \ottkw{case} }}
  | /\                ::   :: meet       {{ tex \wedge }}
  | \/                ::   :: join       {{ tex \vee }}
  | |->               ::   :: ctyp       {{ tex \Vdash }}
  | implies           ::   :: implies    {{ tex \ottkw{implies} }}
  | transp            ::   :: transp     {{ tex \ottkw{transp} }}

formula :: 'formula_' ::=
  | i < j                     ::   :: lt
  | judgement                 ::   :: judgement
  | ( a , S ) in R            ::   :: pmembership
    {{ tex ( [[a]] , [[S]] ) \in [[R]]  }}
  | a in S                    ::   :: tmembership
  | S1 in S2                    ::   :: fmembership
  | formula1 \/ formula2      ::   :: or
  | formula1 /\ formula2      ::   :: and
  | formula1 implies formula2 ::   :: implies
    {{ tex \text{if } [[formula1]]\text{, then } [[formula2]] }}
  | forall a , formula        ::   :: forallTm
    {{ tex \forall [[a]], [[formula]] }}
  | forall S , formula        ::   :: forallSet
    {{ tex \forall [[S]], [[formula]] }}
  | exists S , formula    ::   :: existsSet
    {{ tex \exists [[S]], [[formula]] }}
  | (# formula  #)            ::   :: HParen
    {{ tex [[formula]] }}
  | ( formula )            ::   :: Paren
  | S1 = S2                   ::   :: SetEq
  | ne a                      ::   :: Ne
  | nf a                      ::   :: Nf
  | wn a                      ::   :: Wn
  | wne a                     ::   :: Wne
  | a = b                     ::   :: TmEq
  | [ formula ]              ::   :: Regular
    {{ tex \colorbox{lightgray}{$[[formula]]$} }}
  | x notin fv a             :: :: xnotinfv
   {{ tex [[x]] \notin \ottkw{fv}([[a]]) }}
  | x : A in G  :: :: gmember
   {{ tex [[x]]:[[A]] \in [[G]] }}
  | x notin dom G            ::  :: fresh
   {{ tex [[x]] \notin \mathit{dom}([[G]]) }}

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

embed
{{ tex-preamble
\usepackage{tcolorbox}
\definecolor{lightgray}{gray}{0.85}
}}


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

%%Dependent Types

defns
  JNVal :: '' ::=
defn
  NatOk a :: :: NOk :: 'N_' {{ com natural number values }}
 {{ tex \mathop{ \vdash_\mathbb{N} } [[ a ]] }}
by

----------------- :: Zero
NatOk zero

ne a
--------------- :: Neu
NatOk a

NatOk a
--------------- :: Suc
NatOk succ a


defns
   JPar :: '' ::=

defn
 a => b :: :: Par :: 'P_' {{ com Parallel Reduction }}
 {{ tex [[ a ]] \Rightarrow [[ b ]]  }}
by

----------- :: Var
x => x

---------------- :: Set
Set i => Set i

-------------- :: Void
Void => Void

b0 => b1
---------------------- :: Absurd
absurd b0 => absurd b1

A0 => A1
B0 => B1
----------------------- :: Pi
Pi x:A0 . B0 => Pi x:A1 . B1

a0 => a1
----------------------- :: Abs
\ x. a0 => \ x. a1

a0 => a1
b0 => b1
----------------------- :: App
a0 b0 => a1 b1

a0 => a1
b0 => b1
----------------------- :: AppAbs
(\ x . a0) b0 => a1 { b1 / x }

-------------- :: True
true => true

--------------- :: False
false => false

a0 => a1
b0 => b1
c0 => c1
--------------------------- :: If
if a0 b0 c0 => if a1 b1 c1

b0 => b1
--------------------------- :: IfTrue
if true b0 c0 => b1

c0 => c1
--------------------------- :: IfFalse
if false b0 c0 => c1

-------------- :: Bool
Bool => Bool

a0 => a1
b0 => b1
A0 => A1
----------------------------- :: Eq
a0 ~ b0 : A0 => a1 ~ b1 : A1

------------- :: Refl
refl => refl

c0 => c1
p0 => p1
------------------ :: J
J c0 p0 => J c1 p1

c0 => c1
--------------- :: JRefl
J c0 refl => c1

y notin fv a0
a => a0
----------------------- :: AbsEta
 \ y . ( ( \ x . a)  y)  => a0


defns
JPars :: '' ::=

defn
 a =>+ b :: :: Pars :: 'PS_' {{ com Parallel Reduction }}
 {{ tex [[ a ]] \Rightarrow^* [[ b ]]  }}
by

a => a
---------- :: Refl
a =>+ a

a => b
b =>+ c
------------------ :: Step
a =>+ c


defns
JJoin :: '' ::=

defn
a <=> b :: :: Coherent :: 'C_' {{ com Coherence }}
{{ tex [[a]] \Leftrightarrow [[b]] }}
by

a =>+ c
b =>+ c
----------------------- :: Intro
a <=> b

defns
JTyping :: '' ::=

defn
|- G ::     :: Ctx :: 'Ctx_' {{ com Context Well-Formedness }}
{{ tex \vdash [[G]] }}
by

------------- :: Empty
|- empty


|- G
G |- A : Set i
x notin dom G
------------------------- :: Cons
|- G ++ x : A

defn
  G |- a : A ::  :: Typing :: 'T_' {{ com Typing }}
{{ tex [[G]] \vdash [[a]]: [[A]] }}
 by

|- G
-------------------------- :: Set
G |-  Set i : Set ( Suc i )

|- G
x : A in G
--------------------------------------------- :: Var
G |- x : A

%% functions
G |- A : Set i
G ++ x : A |- B : Set i
--------------------------------------- :: Pi
G |- Pi x : A . B : Set i

G |- Pi x : A . B : Set i
G ++ x : A |- b : B
----------------------------------------------- :: Abs
G |- \ x . b : Pi x : A . B

G |- b : Pi x : A . B
G |- a : A
------------------------------------------- :: App
G |- b a : B { a / x }

G |- a : A
G |- B : Set i
A <=> B
------------------------------- :: Conv
G |- a : B

|- G
G |- a : A
-------------------------------- :: Refl
G |- refl : a ~ a : A

[ G |- A : Set j ]
G |- a : A
G |- b : A
-------------------------------- :: Eq
G |- a ~ b : A : Set i

[ G |- a : A ]
[ G |- b : A ]
[ G |- A : Set j ]
G |- p : a ~ b : A
(# G ++ x : A #) ++ y : a ~ x  : A |- B : Set i
G |- c : B { a ,  refl / x ,  y }
------------------------------------ :: J
G |- J c p : B { b , p / x , y }

|- G
-------------------------- :: Bool
G |- Bool : Set i

|- G
----------------------------- :: True
G |- true : Bool

|- G
----------------------------- :: False
G |- false : Bool

G ++ x : Bool |- A : Set i
G |- a : Bool
G |- b0 : A { true / x }
G |- b1 : A { false / x }
----------------------------- :: If
G |- if a b0 b1 : A { a / x }

|- G
----------------------------- :: Void
G |- Void : Set i

G |- b : Void
G |- A : Set i
----------------- :: Absurd
G |- absurd b : A


|- G
------------------ :: Nat
G |- Nat : Set i

|- G
----------------- :: Zero
G |- zero : Nat

G |- a : Nat
----------------- :: Suc
G |- succ a : Nat


G ++ x : Nat |- A : Set i
G |- a : A { zero / x }
G ++ x : Nat ++ y : A |- b : A { succ x / x }
G |- c : Nat
------------------------------------- :: Ind
G |- ind a (x y . b) c : A { c / x }



defns
JI :: '' ::=

defn
Interp I i A S ::  :: Interp :: 'I_' {{ com Interpretation relation }}
{{ tex \llbracket [[A]] \rrbracket_[[I]]^[[i]] \searrow [[S]] }}
by

ne A
--------------------------- :: Ne
Interp I i A { a | wne a }

--------------------------- :: Void
Interp I i Void emptyset

--------------------------- :: VoidNew
Interp I i Void { a | wne a }

--------------------------- :: Bool
Interp I i Bool { a | a =>+ true \/ a =>+ false }

--------------------------- :: BoolNew
Interp I i Bool { a | a =>+ true \/ a =>+ false \/ wne a  }


------------------------------ :: Nat
Interp I i Nat { a | a =>+ b /\ NatOk b }

--------------------------------------- :: Eq
Interp I i a ~ b : A { p | p =>+ refl /\ a <=> b }

nf a
nf b
nf A
--------------------------------------- :: EqNew
Interp I i a ~ b : A { p | ( p =>+ refl /\ a <=> b )  \/ wne p  }

Interp I i A S
F in S -> PowerSet STm
forall a, (# a in S implies Interp I i B { a / x } F a #)
-------------------------------- :: Pi
Interp I i Pi x : A . B { b | forall a, (# a in S implies b a in F a #) }

Interp I i A S
R in S * PowerSet STm
forall a, (# exists S0  , ( a , S0 ) in R #)
forall a, forall S0, (# ( a , S0 ) in R implies Interp I i B { a / x } S0 #)
-------------------------------- :: PiCoq
Interp I i Pi x : A . B { b | forall a, forall S0, (# ( a , S0 ) in R implies b a in S0 #) }

Interp I i A S
forall a, (# a in S implies (# exists S0 , Interp I i B { a / x } S0 #) #)
------------------------------------------- :: PiAlt
Interp I i Pi x : A . B { b | forall a, (# a in S implies forall S0, (# Interp I i B { a / x } S0 implies  b a in S0 #) #) }

j < i
--------------------------------- :: Set
Interp I i Set j I j

A => B
Interp I i B S
--------------------------------- :: Red
Interp I i A S

defns
JII :: '' ::=

defn
InterpR i A S ::  :: InterpR :: 'IR_' {{ com Interpretation relation }}
{{ tex \llbracket [[A]] \rrbracket^[[i]] \searrow [[S]] }}
by

--------------------------- :: Void
InterpR i Void emptyset

j < i
--------------------------------- :: Set
InterpR i Set j { A | exists S , InterpR j A S }


defns
JIS :: '' ::=

defn
a in InterpR i A :: :: InterpIn :: 'II_' {{ com Membership in interpretation }}
{{ tex [[a]] \in \llbracket [[A]] \rrbracket^{[[i]]} }}
by

InterpR i A S
a in S
---------------- :: In
a in InterpR i A

defns
JIR :: '' ::=

defn
InterpIR i A ::  :: InterpIR :: 'IIR_' {{ com Inductive--recursive interpretaiton }}
{{ tex \llbracket [[A]] \rrbracket^[[i]] }}
by

----------------- :: Void
InterpIR i Void

InterpIR i A
forall a, (# a in InterpIR i A implies InterpIR i B { a / x } #)
---------------------------------------------------------------- :: Pi
InterpIR i Pi x : A . B

j < i
------------------ :: Set
InterpIR i (Set j) 

A => B
InterpIR i B
------------ :: Red
InterpIR i A

defn
a in InterpIR i A :: :: InterpIRIn :: 'IIRIn_' {{ com Membership in interpretation }}
{{ tex [[a]] \in \llbracket [[A]] \rrbracket^{[[i]]} }}
by

defns
JSTyping :: '' ::=

defn
  rho |= G :: :: SubstWff :: 'SSubst_' {{ com SubstWff }}
{{ tex [[rho]] \vDash [[G]] }}
by


defn
  G |= a : A ::  :: STyping :: 'ST_' {{ com Semantic Typing }}
{{ tex [[G]] \vDash [[a]]: [[A]] }}
by


defn
|= G ::     :: SCtx :: 'SCtx_' {{ com Semantic Context Well-Formedness }}
{{ tex {} \vDash [[G]] }}
by

------------- :: Empty
|= empty


|= G
G |= A : Set i
------------------------- :: Cons
|= G ++ x : A
